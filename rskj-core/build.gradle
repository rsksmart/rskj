import java.util.jar.JarEntry
import java.util.jar.JarFile
import java.util.jar.JarOutputStream

plugins {
    id 'application'
}

apply plugin: 'maven'
apply plugin: 'jacoco'

test {
    beforeTest { descriptor ->
        logger.lifecycle("Running test: ${descriptor}")
    }

    jvmArgs '-Xss32m', '-Xmx3G'

    testLogging {
        events "failed"
        exceptionFormat "short"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://deps.rsklabs.io"
    }
}

sourceCompatibility = 1.8

mainClassName = 'co.rsk.Start'
applicationDefaultJvmArgs = ["-server", "-Xss32m", "-Xms3G", "-XX:+UseCompressedOops", "-XX:-OmitStackTraceInFastThrow"]

tasks.withType(JavaCompile){
    options.warnings = false
    options.encoding = 'UTF-8'
    options.compilerArgs << '-XDignore.symbol.file'
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

ext {
    slf4jVersion = '1.7.25'
    leveldbVersion = '0.9'
    junitVersion = '4.12'
    mockitoVersion = '2.23.4'
    powermockitoVersion = '2.0.2'
    rskLllVersion = '0.0.2'
    logbackVersion = '1.2.2'
    bitcoinjVersion = '0.14.4-rsk-9'
    nettyVersion = '4.0.56.Final'
}

dependencies {
    implementation "com.madgag.spongycastle:core:1.52.0.0"
    implementation "io.netty:netty-codec-http:$nettyVersion"
    implementation "org.bouncycastle:bclcrypto-jdk15on:1.59"
    implementation "org.iq80.leveldb:leveldb:${leveldbVersion}"
    implementation "org.fusesource.leveldbjni:leveldbjni:1.8"
    implementation "org.ethereum:leveldbjni-all:1.18.3"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.8.7"
    implementation "org.apache.commons:commons-lang3:3.5"
    implementation "com.typesafe:config:1.3.3"
    implementation "org.mapdb:mapdb:2.0-beta13"
    implementation "co.rsk.bitcoinj:bitcoinj-thin:${bitcoinjVersion}"
    implementation 'com.github.briandilley.jsonrpc4j:jsonrpc4j:1.5.1'
    implementation 'co.rsk:native:1.0.1'
    implementation 'com.sun.xml.ws:jaxws-rt:2.3.3'

    runtimeOnly "org.slf4j:log4j-over-slf4j:${slf4jVersion}"
    runtimeOnly "ch.qos.logback:logback-classic:${logbackVersion}"
    runtimeOnly "ch.qos.logback:logback-core:${logbackVersion}"

    testImplementation "com.googlecode.json-simple:json-simple:1.1.1"
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.powermock:powermock-module-junit4:${powermockitoVersion}"
    testImplementation "org.powermock:powermock-api-mockito2:${powermockitoVersion}"
    testImplementation "co.rsk:lll-compiler:${rskLllVersion}"
    testImplementation "org.awaitility:awaitility:3.0.0"
    testImplementation 'commons-io:commons-io:2.5'
    testImplementation 'commons-codec:commons-codec:1.10'
    testImplementation 'com.squareup.okhttp:okhttp-ws:2.7.2'
}

javadoc {
    options.author = true
    options.header = "RskJ"
    options.addStringOption('-quiet')
    options.encoding = "UTF-8"
}

def generatedResources = "$buildDir/generated-resources"
sourceSets {
    main {
        resources {
            srcDirs += generatedResources
        }
    }
}

task generateResources {
    doLast {
        mkdir generatedResources
        def generated = new File(generatedResources, 'build-info.properties')
        def commitHash = gitCommitHash()
        def currentBranch = gitCurrentBranch()
        generated.text = """
build.hash=$commitHash
build.branch=$currentBranch
"""
    }
}

tasks.withType(Jar) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    dirMode = 0775
    fileMode = 0664
    doLast {
        stripJar(outputs.files.singleFile)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

jar {
    def commitHash = gitCommitHash()
    def currentBranch = gitCurrentBranch()
    manifest {
        attributes  'Git-Commit': commitHash,
                    'Git-Branch': currentBranch
    }
    from sourceSets.main.output.classesDirs
    from sourceSets.main.output.resourcesDir
    from generatedResources
}

task generatePom(dependsOn: jar) {
    def jarName = jar.outputs.files.singleFile.name
    def pomFile = "$libsDir/${jarName.take(jarName.lastIndexOf('.'))}.pom"
    ext.file = new File(pomFile)
    doLast {
        pom {
            project {
                groupId project.group
                artifactId project.name
                version project.version

                licenses {
                    license {
                        name 'GNU General Public License (GPL) version 3.0'
                        url 'http://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }.writeTo(pomFile)
    }
}

task fatJar(type: Jar, dependsOn: jar) {
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }
    exclude "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*.RSA"
    classifier = 'all'
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

tasks.withType(Tar) {
    compression = Compression.GZIP
    extension = 'tar.gz'
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives jar
    archives(generatePom.file) {
        type 'text'
        builtBy generatePom
    }
    archives fatJar
    archives distTar
}

def gitCurrentBranch() {
    def process = "git rev-parse --abbrev-ref HEAD".execute()
    return process.text.trim()
}

def gitCommitHash() {
    def process = "git rev-parse --short HEAD".execute()
    return process.text.trim()
}

def gitDateHash() {
    def process = "git show -s --format=%ct ${gitCommitHash()}".execute()
    return process.text.toLong()
}

def stripJar(File originalFile) {
    def constantTimeForZipEntries = new GregorianCalendar(1980, Calendar.FEBRUARY, 1, 0, 0, 0).timeInMillis
    def reproducibleFile = File.createTempFile("temp", ".tmp")
    reproducibleFile.withOutputStream { fout ->
        def out = new JarOutputStream(fout)
        def jf = new JarFile(originalFile)
        jf.entries().unique {it.name}.sort {it.name}.each {
            def copy = new JarEntry(it.name)
            copy.time = constantTimeForZipEntries
            out.putNextEntry(copy)
            out << jf.getInputStream(it)
        }
        out.finish()
    }
    reproducibleFile.renameTo originalFile
}
