plugins {
    id 'application'
}

apply plugin: 'maven-publish'
apply plugin: 'jacoco'

configurations {
    jmh
}

sourceSets {
    jmh {
        java {
            srcDirs = ['src/jmh/java']
        }
        resources {
            srcDirs = ['src/jmh/resources']
        }
    }
}

testing {
    suites {
        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project
            }
        }
    }
}

test {
    useJUnitPlatform()

    beforeTest { descriptor ->
        logger.lifecycle("Running test: ${descriptor}")
    }

    jvmArgs '-Xss32m', '-Xmx3G'

    testLogging {
        events "failed"
        exceptionFormat "short"
    }
    finalizedBy jacocoTestReport
}

integrationTest {
    dependsOn assemble

    beforeTest { descriptor ->
        logger.lifecycle("Running integration test: ${descriptor}")
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

task parallelTest(type: Test) {
    useJUnitPlatform()
    //increase heap memory
    jvmArgs '-Xss64m', '-Xmx3G'
    //stop build after the first failing test
    failFast = project.hasProperty("failFast")
    //option to use the half of the available cores (it can be increased but there is no too much difference)
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    //this is recommended in the docs, -> https://docs.gradle.org/current/userguide/performance.html#forking_options
    forkEvery = 750
    //save some time avoiding reports
    reports.html.required = false
    reports.junitXml.required = false
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

repositories {
    mavenCentral()
    maven {
        url "https://deps.rsklabs.io"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'co.rsk.Start'
applicationDefaultJvmArgs = ["-server", "-Xss32m", "-Xms3G", "-XX:+UseCompressedOops", "-XX:-OmitStackTraceInFastThrow"]

tasks.withType(JavaCompile) {
    options.warnings = false
    options.encoding = 'UTF-8'
    options.compilerArgs << '-XDignore.symbol.file' // << "-Xlint:unchecked" << "-Xlint:deprecation"
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true

    dirMode = 0775
    fileMode = 0664
}

ext {
    libVersions = [
            nettyVer               : '4.1.78.Final',
            spongyCastleVer        : '1.58.0.0',
            bouncyCastleVer        : '1.59',
            ethereumLeveldbJniVer  : '1.18.3',
            rocksdbJniVer          : '6.27.3',
            slf4jVer               : '1.7.36',
            logbackVer             : '1.2.10',
            jacksonVer             : '2.13.3',
            commonsLang3Ver        : '3.12.0',
            typesafeConfigVer      : '1.4.2',
            mapdbVer               : '2.0-beta13',
            jsonrpc4jVer           : '1.6',
            jaxwsRtVer             : '2.3.5',
            picocliVer             : '4.6.3',

            bitcoinjThinVer: '0.14.4-rsk-14',
            rskjNativeVer: '1.3.0',
    ]

    libs = [
            nettyLib               : "io.netty:netty-codec-http:${libVersions.nettyVer}",
            spongyCastleLib        : "com.madgag.spongycastle:core:${libVersions.spongyCastleVer}",
            bouncyCastleLib        : "org.bouncycastle:bclcrypto-jdk15on:${libVersions.bouncyCastleVer}",
            ethereumLeveldbJniLib  : "org.ethereum:leveldbjni-all:${libVersions.ethereumLeveldbJniVer}",
            rocksdbJniLib        : "org.rocksdb:rocksdbjni:${libVersions.rocksdbJniVer}",
            slf4jApiLib            : "org.slf4j:slf4j-api:${libVersions.slf4jVer}",
            log4jOverSlf4jLib      : "org.slf4j:log4j-over-slf4j:${libVersions.slf4jVer}",
            logbackCoreLib         : "ch.qos.logback:logback-core:${libVersions.logbackVer}",
            logbackClassicLib      : "ch.qos.logback:logback-classic:${libVersions.logbackVer}",
            jacksonLib             : "com.fasterxml.jackson.core:jackson-databind:${libVersions.jacksonVer}",
            commonsLang3Lib        : "org.apache.commons:commons-lang3:${libVersions.commonsLang3Ver}",
            typesafeConfigLib      : "com.typesafe:config:${libVersions.typesafeConfigVer}",
            mapdbLib               : "org.mapdb:mapdb:${libVersions.mapdbVer}",
            jsonrpc4jLib           : "com.github.briandilley.jsonrpc4j:jsonrpc4j:${libVersions.jsonrpc4jVer}",
            jaxwsRtLib             : "com.sun.xml.ws:jaxws-rt:${libVersions.jaxwsRtVer}",
            picocliLib             : "info.picocli:picocli:${libVersions.picocliVer}",

            bitcoinjThinLib        : "co.rsk.bitcoinj:bitcoinj-thin:${libVersions.bitcoinjThinVer}",
            rskjNativeLib          : "co.rsk:native:${libVersions.rskjNativeVer}",
    ]

    testLibVersions = [
            junitVer       : '5.8.1',
            junitSuiteVer  : '1.9.0',
            mockitoVer     : '4.6.1',
            awaitilityVer  : '4.2.0',
            commonsIoVer   : '2.11.0',
            commonsCodecVer: '1.15',
            jacksonVer     : '2.13.3',
            okhttpWsVer    : '2.7.5',

            rskLllVer      : '0.0.2',
    ]

    jmhLibVersions = [
            jmh: "1.36",
            web3j: "4.9.8",
    ]

    testLibs = [
            junitLib                : "org.junit.jupiter:junit-jupiter-engine:${testLibVersions.junitVer}",
            junitParams             : "org.junit.jupiter:junit-jupiter-params:${testLibVersions.junitVer}",
            junitSuite              : "org.junit.platform:junit-platform-suite:${testLibVersions.junitSuiteVer}",
            mockitoLib              : "org.mockito:mockito-inline:${testLibVersions.mockitoVer}",
            mockitoJupiter          : "org.mockito:mockito-junit-jupiter:${testLibVersions.mockitoVer}",
            awaitilityLib           : "org.awaitility:awaitility:${testLibVersions.awaitilityVer}",
            commonsIoLib            : "commons-io:commons-io:${testLibVersions.commonsIoVer}",
            commonsCodecLib         : "commons-codec:commons-codec:${testLibVersions.commonsCodecVer}",
            jacksonLib              : "com.fasterxml.jackson.core:jackson-databind:${testLibVersions.jacksonVer}",
            okhttpWsLib             : "com.squareup.okhttp:okhttp-ws:${testLibVersions.okhttpWsVer}",

            rskLllLib               : "co.rsk:lll-compiler:${testLibVersions.rskLllVer}",
    ]

    jmhLibs = [
            jmhCore                 : "org.openjdk.jmh:jmh-core:${jmhLibVersions.jmh}",
            jmhAnnotations          : "org.openjdk.jmh:jmh-generator-annprocess:${jmhLibVersions.jmh}",
            web3jCore               : "org.web3j:core:${jmhLibVersions.web3j}",
    ]

}

// WARN: consider different setups and sure to not use GPG elements without checksums or without signature file
// in the remote repository (see https://github.com/gradle/gradle/security/advisories/GHSA-j6wc-xfg8-jx2j)
dependencies {
    jmhImplementation "${jmhLibs.jmhCore}"
    jmhImplementation "${jmhLibs.web3jCore}"
    jmhAnnotationProcessor "${jmhLibs.jmhAnnotations}"

    implementation "${libs.nettyLib}"
    implementation "${libs.spongyCastleLib}"
    implementation "${libs.bouncyCastleLib}"
    implementation "${libs.ethereumLeveldbJniLib}"
    implementation "${libs.rocksdbJniLib}"
    implementation "${libs.slf4jApiLib}"
    implementation "${libs.jacksonLib}"
    implementation "${libs.commonsLang3Lib}"
    implementation "${libs.typesafeConfigLib}"
    implementation "${libs.mapdbLib}"
    implementation "${libs.jsonrpc4jLib}"
    implementation "${libs.jaxwsRtLib}"
    implementation "${libs.picocliLib}"

    implementation "${libs.bitcoinjThinLib}"
    implementation "${libs.rskjNativeLib}"

    runtimeOnly "${libs.log4jOverSlf4jLib}"
    runtimeOnly "${libs.logbackCoreLib}"
    runtimeOnly "${libs.logbackClassicLib}"

    testImplementation "${testLibs.junitLib}"
    testImplementation "${testLibs.junitParams}"
    testImplementation "${testLibs.junitSuite}"
    testImplementation "${testLibs.mockitoLib}"
    testImplementation "${testLibs.mockitoJupiter}"
    testImplementation "${testLibs.awaitilityLib}"
    testImplementation "${testLibs.commonsIoLib}"
    testImplementation "${testLibs.commonsCodecLib}"
    testImplementation "${testLibs.okhttpWsLib}"
    testImplementation "${testLibs.rskLllLib}"

    integrationTestImplementation "${testLibs.junitLib}"
    integrationTestImplementation "${testLibs.junitParams}"
    integrationTestImplementation "${testLibs.junitSuite}"
    integrationTestImplementation "${testLibs.mockitoLib}"
    integrationTestImplementation "${testLibs.mockitoJupiter}"
    integrationTestImplementation "${testLibs.jacksonLib}"
    integrationTestImplementation "${testLibs.okhttpWsLib}"
}

javadoc {
    options.author = true
    options.header = "RskJ"
    options.addStringOption('-quiet')
    options.encoding = "UTF-8"
}

def generatedResources = "$buildDir/generated-resources"

publishing {
    publications {
        rskj(MavenPublication) {
            pom {
                from components.java
                licenses {
                    license {
                        name = 'GNU General Public License (GPL) version 3.0'
                        url = 'http://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}

task generateResources {
    def buildInfoFile = 'build-info.properties'

    doLast {
        mkdir generatedResources
        def generated = new File(generatedResources as String, buildInfoFile)
        def commitHash = gitCommitHash()
        def currentBranch = gitCurrentBranch()
        generated.text = """
build.hash=$commitHash
build.branch=$currentBranch
"""
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

jar {
    dependsOn 'generateResources'
    def commitHash = gitCommitHash()
    def currentBranch = gitCurrentBranch()
    manifest {
        attributes 'Git-Commit': commitHash,
                'Git-Branch': currentBranch
    }
    from sourceSets.main.output.classesDirs
    from sourceSets.main.output.resourcesDir
    from generatedResources
}

task generatePom(dependsOn: jar) {
    dependsOn generatePomFileForRskjPublication

    def jarName = jar.outputs.files.singleFile.name
    def pomFile = libsDirectory.dir("${jarName.take(jarName.lastIndexOf('.'))}.pom").get().asFile
    ext.file = pomFile

    doLast {
        copy {
            from "$buildDir/publications/rskj/pom-default.xml"
            into pomFile.parentFile
            rename 'pom-default.xml', pomFile.name
        }
    }
}

task fatJar(type: Jar, dependsOn: jar) {
    archiveClassifier = 'all'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': "$mainClassName"
    }

    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    exclude 'module-info.class'

    for (def jar in configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }) {
        from(jar) {
            eachFile { details -> amendPathIfNeeded(details) }
        }
    }

    with jar
}

task jmhJar(type: Jar) {
    from sourceSets.jmh.output
    archiveClassifier = 'jmh'
}

task jmhE2e(type: JavaExec) {
    classpath = sourceSets.jmh.runtimeClasspath
    mainClass = 'co.rsk.jmh.web3.BenchmarkWeb3RunnerE2E'
}

tasks.withType(Tar) {
    compression = Compression.GZIP
    archiveExtension.set('tar.gz')
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives jar
    archives(generatePom.file) {
        type 'text'
        builtBy generatePom
    }
    archives fatJar
    archives distTar
    archives jmhJar
}

static def gitCurrentBranch() {
    def process = "git rev-parse --abbrev-ref HEAD".execute()
    return process.text.trim()
}

static def gitCommitHash() {
    def process = "git rev-parse --short HEAD".execute()
    return process.text.trim()
}

static def amendPathIfNeeded(details) {
    def uPath = details.path.toUpperCase()
    if (uPath.startsWith("META-INF/LICENSE") || uPath.startsWith("META-INF/NOTICE")) {
        def originalFile = details.file as File
        def jarName = originalFile.parentFile.parentFile.name.split(".jar")[0]
        def newPath = originalFile.parentFile.name + "/" + jarName + "_" + originalFile.name
        details.path = newPath
    }
}
